;; (tokenize "typedef unsigned long ULONG, *ULONG_PTR, ** ULONG_PTR_PTR")
;; (tokenize "C:/Program Files/Microsoft SDKs/Windows/v6.0A/Include/winhttp.h" :file t)

(defun tokenize-string (string)
  (tokenize string))

(defun tokenize-file (file)
  (tokenize file :file t))

(defun tokenize (string-or-file &key file)
  (let ((b (get-buffer-create "*temp*")))
    (unwind-protect
        (progn
          (set-buffer b)
          (if file
              (insert-file-contents string-or-file)
            (insert string-or-file))
          (goto-char (point-min))
          (let (r token)
            (loop
              (push (or (next-token)
                        (return))
                    r))
            (nreverse r)))
      (delete-buffer b))))

(defun next-token ()
  (let ((column (current-column))
        (lineno (current-line-number)))
    (multiple-value-bind (type token)
        (scan-token)
      (when token
        (list lineno column type token)))))

(defun scan-token ()
  (let ((opoint (point)))
    (flet ((substr ()
             (buffer-substring opoint (point)))
           (forward-matched ()
             (forward-char (length (match-string 0))))
           (looking-number (radix)
             (funcall (if (eq (match-string 1) "-") '- '+)
                      (parse-integer (match-string 2) :radix radix)))
           (current-char ()
             (forward-char)
             (char-before (point))))
      (cond ((eobp)
             (values nil nil))
            ((skip-chars-forward " \t\n")
             (values :whitespace (substr)))
            ((looking-for "//")
             (goto-eol)
             (values :comment (substr)))
            ((looking-for "/*")
             (unless (scan-buffer "*/" :tail t)
               (error "unterminated comment (*/ not found)"))
             (values :comment (substr)))
            ((looking-at "#[a-z]+")
             (forward-matched)
             (values :directive (substr)))
            ((looking-at "\\(\\*+\\)?[ \t]*\\([a-zA-Z_][a-zA-Z0-9_]*\\)")
             (forward-matched)
             (values :ident (if (match-string 1)
                                (list (intern (match-string 1))
                                      (intern (match-string 2)))
                              (intern (match-string 2)))))
            ((looking-at "\\([+-]?[0-9]*\\.[0-9]\\([eE][0-9]+\\)?\\)[FDfd]*")
             (forward-matched)
             (values :number (read-from-string (match-string 1))))
            ((looking-at "\\([+-]?\\)0x\\([0-9a-fA-F]+\\)[ULul]*")
             (forward-matched)
             (values :number (looking-number 16)))
            ((looking-at "\\([+-]?\\)0\\([0-9]+\\)[ULul]*")
             (forward-matched)
             (values :number (looking-number 8)))
            ((looking-at "\\([+-]?\\)\\([1-9][0-9]*\\)[ULul]*")
             (forward-matched)
             (values :number (looking-number 10)))
            (t
             (values :punc (current-char)))
            ))))
